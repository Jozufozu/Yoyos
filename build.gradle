buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

version = '2.0.1.5'
group = 'com.jozufozu.yoyos' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'yoyos'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = compileKotlin.kotlinOptions.jvmTarget = compileTestKotlin.kotlinOptions.jvmTarget = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: '20191122-1.14.3'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // enable console colors
            properties 'forge.logging.noansi': 'false'

            mods {
                yoyos {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // enable console colors
            properties 'forge.logging.noansi': 'false'

            mods {
                yoyos {
                    source sourceSets.main
                }
            }
        }
    }
}


repositories {
    mavenCentral()
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }

    maven {
        url = 'https://minecraft.curseforge.com/api/maven/'
    }

    maven {
        url = "https://maven.theillusivec4.top/"
    }

    maven {
        url = "https://maven.blamejared.com"
    }
}

configurations {
    mod
}

dependencies {
    minecraft "net.minecraftforge:forge:$mc_version-$forge_version"

    compileOnly fg.deobf("mezz.jei:jei-$mc_version:$jei_version:api")
    runtimeOnly fg.deobf("mezz.jei:jei-$mc_version:$jei_version")

    compile "kottle:Kottle:$kottle_version"
    mod "kottle:Kottle:$kottle_version"

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-1.14.4-1.0.2")
    compileOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-1.14.4-1.0.2:api")

    compile fg.deobf("vazkii.patchouli:Patchouli:1.1-23.115")
    compileOnly fg.deobf("vazkii.botania:Botania:r1.11-373.177")
    runtimeOnly fg.deobf("vazkii.botania:Botania:r1.11-373.177")
}

task installMods(type: Copy, dependsOn: "deinstallMods") {
    from { configurations.mod }
    include "**/*.jar"
    into file("run/mods")
}

task deinstallMods(type: Delete) {
    delete fileTree(dir: "run/mods", include: "*.jar")
}

project.afterEvaluate {
    project.tasks['prepareRuns'].dependsOn(project.tasks['installMods'])
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "." + extension

    manifest {
        attributes([
            "Specification-Title": "yoyos",
            "Specification-Vendor": "Jozufozu",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Jozufozu",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.exe"
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "-sources." + extension
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveName = baseName + "_" + project.mc_version + "-" + project.version  + "-deobf." + extension
}

// Tell the artifact system about our extra jars
artifacts {
    archives srcJar, deobfJar
}

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}